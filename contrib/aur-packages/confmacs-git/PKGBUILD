# Maintainer: Severen Redwood <severen.redwood@gmail.com>
# Report all package issues to `https://github.com/SShrike/arch-packages`

_pkgname='confmacs'
pkgname='confmacs-git'
pkgver=r30.286575c
pkgrel=1
pkgdesc='An Emacs configuration switcher'
arch=('any')
license=('GPL3')
makedepends=('git')
depends=('rubygems' 'ruby-thor' 'ruby-colorize' 'ruby-highline')
conflicts=('confmacs')
provides=('confmacs')
options=(!emptydirs)
url="http://rubygems.org/gems/${pkgname}"
source=('git+https://github.com/SShrike/confmacs.git')
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_pkgname}"
  rake build
}

package() {
  cd "${srcdir}/${_pkgname}"
  local _gemdir=$(ruby -e 'puts Gem.default_dir')
  gem install --ignore-dependencies --no-user-install pkg/${_pkgname}-*.gem -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" pkg/${_pkgname}-*.gem
  find ${pkgdir} -name \*.gem -type f -delete
}
